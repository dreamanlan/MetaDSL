//From: https://github.com/dreamanlan/MetaDSL/blob/29e88dc3dca699d8236eed4fb63314624bf90155/DslParser/dsl.txt
//For: https://www.bottlecaps.de/rr/ui

PROGRAM::=
	STATEMENTS

STATEMENTS::=
	STATEMENT
	| STATEMENTS SEP STATEMENT

STATEMENT::=
	OPERATOR_STATEMENT_DESC_0

//OPERATOR_STATEMENT_0::=
//	OPERATOR_STATEMENT_DESC_0

OPERATOR_STATEMENT_1::=
	OPERATOR_STATEMENT_DESC_1

OPERATOR_STATEMENT_2::=
	OPERATOR_STATEMENT_DESC_2

OPERATOR_STATEMENT_COLON::=
	OPERATOR_STATEMENT_DESC_COLON

//OPERATOR_STATEMENT_3::=
//	OPERATOR_STATEMENT_DESC_3

OPERATOR_STATEMENT_4::=
	OPERATOR_STATEMENT_DESC_4

OPERATOR_STATEMENT_5::=
	OPERATOR_STATEMENT_DESC_5

OPERATOR_STATEMENT_6::=
	OPERATOR_STATEMENT_DESC_6

OPERATOR_STATEMENT_7::=
	OPERATOR_STATEMENT_DESC_7

OPERATOR_STATEMENT_8::=
	OPERATOR_STATEMENT_DESC_8

OPERATOR_STATEMENT_9::=
	OPERATOR_STATEMENT_DESC_9

OPERATOR_STATEMENT_10::=
	OPERATOR_STATEMENT_DESC_10

OPERATOR_STATEMENT_11::=
	OPERATOR_STATEMENT_DESC_11

OPERATOR_STATEMENT_12::=
	OPERATOR_STATEMENT_DESC_12

OPERATOR_STATEMENT_13::=
	OPERATOR_STATEMENT_DESC_13

OPERATOR_STATEMENT_14::=
	OPERATOR_STATEMENT_DESC_14

//OPERATOR_STATEMENT_15::=
//	OPERATOR_STATEMENT_DESC_15

//Left Associative
OPERATOR_STATEMENT_DESC_0::=
	OPERATOR_STATEMENT_DESC_1
	| OPERATOR_STATEMENT_DESC_0 OP_TOKEN_0 OPERATOR_STATEMENT_1

//Right Associative (assignment, condition expression)
OPERATOR_STATEMENT_DESC_1::=
	OPERATOR_STATEMENT_DESC_2
	| OPERATOR_STATEMENT_DESC_2 OPERATOR_STATEMENT_DESC_ASSIGNMENT_OR_QUESTION_COLON

OPERATOR_STATEMENT_DESC_ASSIGNMENT_OR_QUESTION_COLON::=
	OP_TOKEN_1 OPERATOR_STATEMENT_1
	| OP_TOKEN_QUESTION OPERATOR_STATEMENT_1 OP_TOKEN_QUESTION_COLON OPERATOR_STATEMENT_1

//Right Associative (=>, <-)
OPERATOR_STATEMENT_DESC_2::=
	OPERATOR_STATEMENT_DESC_COLON
	| OPERATOR_STATEMENT_DESC_COLON OP_TOKEN_2 OPERATOR_STATEMENT_2

//Right Associative (type info)
OPERATOR_STATEMENT_DESC_COLON::=
	OPERATOR_STATEMENT_DESC_3
	| OPERATOR_STATEMENT_DESC_3 OP_TOKEN_COLON OPERATOR_STATEMENT_COLON

//Left Associative
OPERATOR_STATEMENT_DESC_3::=
	OPERATOR_STATEMENT_DESC_4
	| OPERATOR_STATEMENT_DESC_3 OP_TOKEN_3 OPERATOR_STATEMENT_4

//Left Associative
OPERATOR_STATEMENT_DESC_4::=
	OPERATOR_STATEMENT_DESC_5
	| OPERATOR_STATEMENT_DESC_4 OP_TOKEN_4 OPERATOR_STATEMENT_5

//Left Associative
OPERATOR_STATEMENT_DESC_5::=
	OPERATOR_STATEMENT_DESC_6
	| OPERATOR_STATEMENT_DESC_5 OP_TOKEN_5 OPERATOR_STATEMENT_6

//Left Associative
OPERATOR_STATEMENT_DESC_6::=
	OPERATOR_STATEMENT_DESC_7
	| OPERATOR_STATEMENT_DESC_6 OP_TOKEN_6 OPERATOR_STATEMENT_7

//Left Associative
OPERATOR_STATEMENT_DESC_7::=
	OPERATOR_STATEMENT_DESC_8
	| OPERATOR_STATEMENT_DESC_7 OP_TOKEN_7 OPERATOR_STATEMENT_8

//Left Associative
OPERATOR_STATEMENT_DESC_8::=
	OPERATOR_STATEMENT_DESC_9
	| OPERATOR_STATEMENT_DESC_8 OP_TOKEN_8 OPERATOR_STATEMENT_9

//Left Associative
OPERATOR_STATEMENT_DESC_9::=
	OPERATOR_STATEMENT_DESC_10
	| OPERATOR_STATEMENT_DESC_9 OP_TOKEN_9 OPERATOR_STATEMENT_10

//Left Associative
OPERATOR_STATEMENT_DESC_10::=
	OPERATOR_STATEMENT_DESC_11
	| OPERATOR_STATEMENT_DESC_10 OP_TOKEN_10 OPERATOR_STATEMENT_11

//Left Associative
OPERATOR_STATEMENT_DESC_11::=
	OPERATOR_STATEMENT_DESC_12
	| OPERATOR_STATEMENT_DESC_11 OP_TOKEN_11 OPERATOR_STATEMENT_12

//Left Associative
OPERATOR_STATEMENT_DESC_12::=
	OPERATOR_STATEMENT_DESC_13
	| OPERATOR_STATEMENT_DESC_12 OP_TOKEN_12 OPERATOR_STATEMENT_13

//Left Associative
OPERATOR_STATEMENT_DESC_13::=
	OPERATOR_STATEMENT_DESC_14
	| OPERATOR_STATEMENT_DESC_13 OP_TOKEN_13 OPERATOR_STATEMENT_14

//Right Associative (unary operator)
OPERATOR_STATEMENT_DESC_14::=
	OPERATOR_STATEMENT_DESC_15
	| OPERATOR_STATEMENT_DESC_15 OP_TOKEN_14 OPERATOR_STATEMENT_14

//Left Associative (member operator like)
OPERATOR_STATEMENT_DESC_15::=
	FUNCTION_STATEMENT_DESC
	| OPERATOR_STATEMENT_DESC_15 OP_TOKEN_15 FUNCTION_STATEMENT

FUNCTION_STATEMENT::=
	FUNCTION_STATEMENT_DESC

FUNCTION_STATEMENT_DESC::=
	FUNCTION_CALLS

FUNCTION_CALLS::=
	FUNCTION_EX_CALL_zom
	| FUNCTION_PARAMS FUNCTION_EX_CALL_zom

FUNCTION_EX_CALL_zom::=
	/*empty*/
	| FUNCTION_EX_CALL_zom FUNCTION_EX_CALL

FUNCTION_EX_CALL::=
	FUNCTION_ID
	| FUNCTION_ID NULLABLE_FUNCTION_PARAMS

NULLABLE_FUNCTION_PARAMS::=
	OP_TOKEN_NULLABLE FUNCTION_PARAMS
	| FUNCTION_PARAMS

FUNCTION_PARAMS::=
	PARENTHESES_PARAMS
	| MEMBER

PARENTHESES_PARAMS::=
	PARENTHESES_PARAMS0
	| PARENTHESES_PARAMS0 NULLABLE_FUNCTION_PARAMS

PARENTHESES_PARAMS0::=
	'(' STATEMENTS ')'
	| '[' STATEMENTS ']'
	| '{' STATEMENTS '}'
	| SCRIPT_CONTENT
	| BRACKET_COLON_BEGIN STATEMENTS BRACKET_COLON_END
	| PARENTHESES_COLON_BEGIN STATEMENTS PARENTHESES_COLON_END
	| ANGLE_BRACKET_COLON_BEGIN STATEMENTS ANGLE_BRACKET_COLON_END
	| BRACE_PERCENT_BEGIN STATEMENTS BRACE_PERCENT_END
	| BRACKET_PERCENT_BEGIN STATEMENTS BRACKET_PERCENT_END
	| PARENTHESES_PERCENT_BEGIN STATEMENTS PARENTHESES_PERCENT_END
	| ANGLE_BRACKET_PERCENT_BEGIN STATEMENTS ANGLE_BRACKET_PERCENT_END

MEMBER::=
	MEMBER0
	| MEMBER0 NULLABLE_FUNCTION_PARAMS

MEMBER0::=
	"::" MEMBER_PARAM
	| '.' MEMBER_PARAM
	| POINTER MEMBER_PARAM
	| PERIOD_STAR MEMBER_PARAM
	| POINTER_STAR MEMBER_PARAM

MEMBER_PARAM::=
	FUNCTION_ID
	| '(' STATEMENTS ')'
	| '[' STATEMENTS ']'
	| '{' STATEMENTS '}'
	| SCRIPT_CONTENT
	| BRACKET_COLON_BEGIN STATEMENTS BRACKET_COLON_END
	| PARENTHESES_COLON_BEGIN STATEMENTS PARENTHESES_COLON_END
	| ANGLE_BRACKET_COLON_BEGIN STATEMENTS ANGLE_BRACKET_COLON_END
	| BRACE_PERCENT_BEGIN STATEMENTS BRACE_PERCENT_END
	| BRACKET_PERCENT_BEGIN STATEMENTS BRACKET_PERCENT_END
	| PARENTHESES_PERCENT_BEGIN STATEMENTS PARENTHESES_PERCENT_END
	| ANGLE_BRACKET_PERCENT_BEGIN STATEMENTS ANGLE_BRACKET_PERCENT_END

FUNCTION_ID::=
	IDENTIFIER
	| STRING
	| NUMBER
	| DOLLAR_STRING

SEP::=
	','
	| ';'

//Tokens

ANGLE_BRACKET_COLON_BEGIN ::= "<:"
ANGLE_BRACKET_COLON_END ::= ":>"
ANGLE_BRACKET_PERCENT_BEGIN ::= "<%"
ANGLE_BRACKET_PERCENT_END ::= "%>"
BRACE_PERCENT_BEGIN ::= "{%"
BRACE_PERCENT_END ::= "%}"
BRACKET_COLON_BEGIN ::= "[:"
BRACKET_COLON_END ::= ":]"
BRACKET_PERCENT_BEGIN ::= "[%"
BRACKET_PERCENT_END ::= "%]"
OP_TOKEN_0 ::= "`"
OP_TOKEN_1 ::= "="|"+="|"-="|"*="|"/="|"%="|"&="|"|="|"^="|">>="|"<<="|">>>="
OP_TOKEN_2 ::= "=>"|"<-"
OP_TOKEN_4 ::= "||"|"??"
OP_TOKEN_5 ::= "&&"
OP_TOKEN_6 ::= "|"
OP_TOKEN_7 ::= "^"
OP_TOKEN_8 ::= "&"
OP_TOKEN_9 ::= "=="|"!="|"<=>"
OP_TOKEN_10 ::= "<="|">="|"<"|">"
OP_TOKEN_11 ::= "<<"|">>"|">>>"
OP_TOKEN_12 ::= "+"|"-"
OP_TOKEN_13 ::= "*"|"/"|"%"|".."
OP_TOKEN_14 ::= "++"|"--"|"~"|"!"
OP_TOKEN_COLON ::= ":"
OP_TOKEN_QUESTION ::= "?"
OP_TOKEN_QUESTION_COLON ::= "-:"
OP_TOKEN_NULLABLE ::= "-?"|"-!"
PARENTHESES_COLON_BEGIN ::= "(:"
PARENTHESES_COLON_END ::= ":)"
PARENTHESES_PERCENT_BEGIN ::= "(%"
PARENTHESES_PERCENT_END ::= "%)"
PERIOD_STAR ::= ".*"
POINTER ::= "->"
POINTER_STAR ::= "->*"
